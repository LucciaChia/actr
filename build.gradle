plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'biz.aQute.bnd.builder' version '6.3.1'
    id 'org.sonarqube' version '3.4.0.2513'
}

def ossrhUser = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername')
def ossrhPass = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword')

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 11
targetCompatibility = 11

group = 'com.github.zakgof'
archivesBaseName = 'actr'
version = '0.4.3-SNAPSHOT'

ext {
   descr = 'Simple Java actor model implementation'
}

repositories {
    mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
	// https://mvnrepository.com/artifact/commons-codec/commons-codec
	implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
	
}

sourceSets {
   test {
      java {
        srcDirs = ['src/example/java', 'src/test/java']
      }
   }
}

jar {
  bnd('-exportcontents': 'com.zakgof.actr')
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifactId = archivesBaseName
            groupId = group
            version = version
            from(components["java"])
            pom {
                name = 'actr'
                description = 'Simple Java actor model implementation'
                url = 'https://github.com/zakgof/actr'

                scm {
                    connection = 'scm:git:https://github.com/zakgof/actr.git'
                    developerConnection = 'scm:git:https://github.com/zakgof/actr.git'
                    url = 'https://github.com/zakgof/actr/tree/master/actr'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'zakgof'
                        name = 'Oleksandr Zakusylo'
                        email = 'zakgof@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = ossrhUser
                password = ossrhPass
            }
        }
        mavenLocal()
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "zakgof_actr"
        property "sonar.organization", "zakgof"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", project.hasProperty('sonarlogin') ? sonarlogin : null
    }
}

tasks['sonarqube'].dependsOn test
